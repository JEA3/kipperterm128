10 REM GOPHER FORMAT ADDRESS BOOK EDITOR
11 REM JONNOSAN AT GMAIL DOT COM
12 REM 2010-07-30
20 D=PEEK(186):REM LAST USED DRIVE
30 NE=16:REM NUMBER OF ENTRIES
35 T$=CHR$(9)
37 NL$=CHR$(13)+CHR$(10)
40 DIM TY$(NE)
50 DIM DS$(NE)
70 DIM HO$(NE)
80 DIM PO(NE)
90 GOSUB 800
95 PRINT
100 A$="ADDRESSES.TXT"
110 PRINT "READING ";A$
200 OPEN 2,D,2,A$
210 GOSUB 10000
220 IF EN=0 THEN GOTO 300 :REM No troubles opening file
240 PRINT EM$
245 GOSUB 13000:REM wait for key
250 GOTO 1000
300 I=0
310 I=I+1
315 PRINT "ENTRY ";CHR$(I+64) 
320 IF I>NE THEN GOTO 1000
330 GET#2,P$
340 IF P$=""GOTO 1000 : rem 4 parts of entry
350 GOSUB 12000:DS$(I)=S$  :Rem Description
360 GOSUB 12000:SE$=S$: rem not sure (TYPE Gopher, VT100, PETSCII etc)
370 GOSUB 12000:HO$(I)=S$ : REM host
380 GOSUB 12000:PO(I)=VAL(S$): rem port
400 IF P$="1" THEN TY$(I)="G":GOTO 310: REM First byte of entry is the type 1 apparently is GOPHER?
410 IF P$<>"8"GOTO 310
420 IF LEFT$(SE$,1)="/" THEN TY$(I)="P":GOTO 310
430 TY$(I)="A"
500 GOTO 310
800 PRINTCHR$(147)
810 PRINT"       KIPPER ADDRESS BOOK EDITOR"
815 PRINT
820 RETURN
1000 GOSUB 800
1002 PRINT "ENTRY DESCRIPTION HOSTNAME    PORT"
1005 FOR I=1TONE
1010 PRINT" ";CHR$(I+64);" ";
1020 PRINT TY$(I);" ";
1030 PRINT LEFT$(DS$(I)+"            ",12);" ";
1040 PRINT LEFT$(HO$(I)+"            ",12);" ";
1050 IF PO(I)>0 THEN PRINT PO(I);
1090 PRINT
1100 NEXT I
1104 PRINT
1105 PRINT "ENTER A LETTER TO EDIT ENTRY"
1107 PRINT "OR Q TO QUIT, S TO SAVE"
1110 GET C$
1115 IF C$=""GOTO1110
1116 IF C$="Q" THEN GOTO 14000
1117 IF C$="S" THEN GOTO 15000
1120 S=ASC(C$)-64
1130 IF S>NE GOTO 1110
1140 IF S<1 GOTO 1110
1200 GOSUB 800
1204 PRINT"A=ASCII/P=PETSCII/G=GOPHER";
1210 IF TY$(S)<>"" THEN PRINT "<";TY$(S);">";
1230 GET C$
1235 IF C$=""GOTO1230
1240 IF ASC(C$)=13 GOTO 1300
1250 IF C$="A" THEN TY$(S)="A":GOTO1300
1260 IF C$="P" THEN TY$(S)="P":GOTO1300
1270 IF C$="G" THEN TY$(S)="G":GOTO1300
1280 GOTO 1230
1300 IF TY$(S)=""THEN GOTO 1000
1310 IF TY$(S)="A" THEN PRINT " ASCII":GOTO 1350
1315 IF TY$(S)="P" THEN PRINT " PETSCII":GOTO 1350
1320 PRINT " GOPHER"
1350 PRINT "DESCRIPTION <";DS$(S);">";
1360 I$="":INPUT I$
1370 IF I$<>""THEN DS$(S)=I$
1400 PRINT "HOSTNAME <";HO$(S);">";
1410 I$="":INPUT I$
1420 IF I$<>""THEN HO$(S)=I$
1450 PRINT "PORT <";PO(S);">";
1460 INPUT PO(S)
1800 GOTO 1000
9999 END
10000 OPEN 15,D,15: REM read error channel
10010 INPUT#15,EN,EM$,ET,ES
10020 CLOSE 15
10030 RETURN
10100 GOSUB 10000 : Print OUT THE ERROR CHANNEL
10110 PRINT EN,EM$,ET,ES
10120 RETURN
12000 S$="" : rem build up the string from the disk data s$ holds the string
12001 IF STATUS AND 64 THEN 12900
12010 GET#2,C$
12020 IF STATUS AND 64 THEN 12900
12030 IF C$=T$GOTO 12900
12040 IF ASC(C$)>13 GOTO 12100
12050 GET#2,C$:REM SKIP THE LF AFTER CR
12060 GOTO 12900
12100 S$=S$+C$
12110 GOTO 12010
12900 RETURN
13000 PRINT "HIT ANY KEY TO CONTINUE"
13010 GET C$
13020 IF C$=""GOTO13010
13030 RETURN
14000 PRINT "REALLY QUIT? <Y/N>"
14010 GET C$
14020 IF C$="Y" THEN END
14030 IF C$="N" THEN GOTO 1000
14040 GOTO 14010
15000 CLOSE 2
15001 PRINT"ENTRY ";A$
15005 OPEN 2,D,2,"@:"+A$+",S,W"
15010 GOSUB 10000
15020 IF EN=0 THEN GOTO 15100
15030 PRINT EM$
15040 GOSUB 13000
15050 GOTO 1000
15100 FOR I=1TONE
15110 PRINT "ENTRY ";CHR$(I+64);" ";
15120 IF TY$(I)=""GOTO 15300
15130 IF DS$(I)=""GOTO 15300
15140 IF HO$(I)=""GOTO 15300
15150 IF PO(I)=0 GOTO 15300
15160 IF TY$(I)="G" THEN PRINT#2,"1";DS$(I);T$;"/";
15170 IF TY$(I)="A" THEN PRINT#2,"8";DS$(I);T$;"ASCII";
15180 IF TY$(I)="P" THEN PRINT#2,"8";DS$(I);T$;"/";CHR$(110);
15185 P$=STR$(PO(I))
15186 PO$=MID$(P$,2,LEN(P$)-1)
15190 PRINT#2,T$;HO$(I);T$;PO$;NL$;
15200 PRINT "SAVED"
15290 GOTO 15600
15300 PRINT "SKIPPED - INCOMPLETE"
15600 NEXT I
15610 GOSUB 13000
15616 CLOSE 2
15620 GOTO 1000
20000 SAVE"@:ABE",8
20010 END



